<job id="giipAgent ver 1.52">
<script language="VBScript" src="../giipAgent.cfg"></script>
<script language="VBScript">
' -------------------------------------------------------------
' Authorized by Lowy at 20160805
' v1.5 (170616)
' Support Powershell ps1 script
' v1.1 (160805)
' execute all queue at once
' v1.04
' - Add OS Check as default
' v1.03
' - kill wscript process force
' v1.02 (150626)
' - hide command window
' -------------------------------------------------------------
' -------------------------------------------------------------
' Initialize
On Error Resume Next
scrver = 1.52
' User Variables 

strComputer = "." 
Set objWMIService = GetObject("winmgmts:\\" & strComputer & "\root\CIMV2") 
' OS ====================================================
Set colItems = objWMIService.ExecQuery( _
    "SELECT * FROM Win32_OperatingSystem",,48) 
For Each objItem in colItems 
    OSName = objItem.Caption
Next

' -------------------------------------------------------------
' System Variables
lwURLAPI = "https://giipaspstg02.azurewebsites.net/api/cqe/cqequeueget04.asp?sk={{sk}}&lssn={{lssn}}&os=" & OSName & "&df=os&sv=" & scrver
lwPathLog = "../giipLogs"
lwLogFileName = "giipAgent_" & SetDtToStr(now(), "YYYYMMDD") & ".log"

' -------------------------------------------------------------
' Processing Variables
Set oWshShell = CreateObject("WScript.Shell")
lwPath= oWshShell.CurrentDirectory
lwURLAPI = replace(lwURLAPI, "{{sk}}", at)
lwURLAPI = replace(lwURLAPI, "{{lssn}}", lsSn)

Set oFSO = CreateObject("Scripting.FileSystemObject")
If Not oFSO.FolderExists(lwPathLog) Then
  oFSO.CreateFolder lwPathLog
End If

' Get & Execute Scripts
'msgbox lwURLAPI
lwHTTPRst = lwGetHTTP (lwURLAPI, "GET", "", "utf-8", "text")

lwLogFileWrite lwPathLog, lwLogFileName, "[" & SetDtToStr(now(), "YYYY/MM/DD HH24:MI:SS") & "] " & lwURLAPI & vbCRLF
lwLogFileWrite lwPathLog, lwLogFileName, "[" & SetDtToStr(now(), "YYYY/MM/DD HH24:MI:SS") & "] Check from giip SQE..." & lwScrType & vbCRLF

lpi = 0
do while lwHTTPRst <> empty and lpi < 1

	aryHTTPRst = split(lwHTTPRst, "||")

	if ubound(aryHTTPRst) = 2 then
		lwScrSn = aryHTTPRst(0)
		lwScrType = aryHTTPRst(1)
		lwScriptBody = aryHTTPRst(2)
		lwScriptBody = replace(lwScriptBody , "{{sk}}", at)
		lwScriptBody = replace(lwScriptBody , "{{lssn}}", lsSn)
		ExecQue lwPath, lwScriptBody
		lwLogFileWrite lwPathLog, lwLogFileName, "[" & SetDtToStr(now(), "YYYY/MM/DD HH24:MI:SS") & "] Que Executed!! qsn:" & lwScrSn & " Script Type:" & lwScrType & vbCRLF
	else
		lwLogFileWrite lwPathLog, lwLogFileName, "[" & SetDtToStr(now(), "YYYY/MM/DD HH24:MI:SS") & "] (" & ubound(aryHTTPRst) & ")" & lwHTTPRst & vbCRLF
	end if

	lwHTTPRst = lwGetHTTP (lwURLAPI, "GET", "", "utf-8", "text")
	lpi = lpi + 1
loop

Set oWshShell = Nothing

Sub ExecQue(lwPath, lwText)
	Dim lwFso, sAbsFileName, DT, sDT
	lwTmpFileName = "tmpScript." & lwScrType
	sAbsFileName = lwPath & "\" & lwTmpFileName
	lwTextSetLog = lwTmpFileName
	lwBatTmp = lwPath & "\lwTmpScriptExec.bat"

	Set lwFso = WScript.CreateObject("Scripting.FileSystemObject")

	if lwFso.fileexists(sAbsFileName) then
		lwFso.DeleteFile(sAbsFileName)
	end if
	if lwFso.fileexists(lwBatTmp) then
		lwFso.DeleteFile(lwBatTmp)
	end if
	' save Temp File
	lwLogFileWrite lwPath, lwTmpFileName, lwText
	lwLogFileWrite lwPath, "lwTmpScriptExec.bat", lwTextSetLog

	Select Case lwScrType
	Case "wsf"
		runcmd = "cmd.exe /c start /min " & lwTmpFileName & " ^& exit"
		runcmd = "wscript //B //Nologo " & lwTmpFileName
		Call oWshShell.run(runcmd)
	Case "ps1"
		runcmd = "powershell.exe """ & lwPath & "\" & lwTmpFileName & """"
		Call oWshShell.run(runcmd)
	Case else
		Call oWshShell.run(lwTmpFileName)
	End Select

	WScript.Sleep 10000

	Set wmi = GetObject("winmgmts://./root/cimv2")

	qry = "SELECT * FROM Win32_Process WHERE Name='wscript.exe' AND NOT " & _
      		"CommandLine LIKE '%" & Replace(WScript.ScriptFullName, "\", "\\") & "%'"

	For Each p In wmi.ExecQuery(qry)
	  p.Terminate
	Next

	if lwFso.fileexists(lwBatTmp) then
		lwFso.DeleteFile(lwBatTmp)
	end if
	if lwFso.fileexists(sAbsFileName) then
	'	lwFso.DeleteFile(sAbsFileName)
	end if

      Set lwFso = Nothing
End Sub

Sub lwLogFileWrite(sPath, sFileName, sContent)
      Set lwFso = CreateObject("Scripting.FileSystemObject")
      if lwFso.FileExists(sPath & "\" & sFileName) then
            set lwLogFile = lwFso.opentextfile(sPath & "\" & sFileName, 8, true)
      else
            set lwLogFile =  lwFso.createtextfile(sPath & "\" & sFileName, true)
      end if

      lwLogFile.Write sContent
      lwLogFile.close
End Sub

Function lwGetHTTP(url, meth, fv, charset, output)
 Dim xmlHttp
 Set xmlHttp = CreateObject("MSXML2.serverXMLHTTP")
 xmlHttp.Open meth, url, False
	if charset = "utf-8" then
		xmlHttp.setRequestHeader "Content-Type", " text/html; charset=utf-8"
	else
		xmlHttp.setRequestHeader "Content-Type", " text/html"
	end if
 'xmlHttp.setRequestHeader "Content-Length", "length"
 if fv = empty then
   xmlHttp.Send
 else
   xmlHttp.Send fv
 end if
 txtData = xmlHttp.responseText
 htmlData = xmlHttp.responsebody
	if output = "html" then
		lwGetHTTP = htmlData
	else
		lwGetHTTP = txtData
	end if
End Function

Function SetDtToStr(dt, date_type)

      Dim mydate
      date_type = Ucase(date_type)
      if isdate(dt) then

            hour12 = hour(dt)
            if cint(hour12) > 12 then
                  hour12 = hour12 - 12
            end if
            mydate = replace (date_type, "YYYY", year(dt))
            mydate = replace (mydate, "YY", right(year(dt), 2))
            mydate = replace (mydate, "MM", right("0" & month(dt),2))
            mydate = replace (mydate, "DD", right("0" & day(dt),2))
            mydate = replace (mydate, "HH24", right("0" & hour(dt),2))
            mydate = replace (mydate, "HH", hour12)
            mydate = replace (mydate, "MI", right("0" & minute(dt),2))
            mydate = replace (mydate, "SS", right("0" & second(dt),2))

      else
     
            mydate = "1999/01/01 00:00:00"
     
      end if

      SetDtToStr = mydate

End Function

</script>
</job>

